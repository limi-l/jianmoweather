plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.google.ksp
    alias libs.plugins.google.hilt
    alias libs.plugins.kotlin.kapt
}

def keystorePropertiesFile = rootProject.file("keystore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'dev.shuanghua.weather'
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        applicationId "dev.shuanghua.weather"
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode libs.versions.versionCode.get().toInteger()
        versionName libs.versions.versionName.get()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile rootProject.file("keystore/shuanghua.jks")
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }

        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeCompiler.get()
    }
}

dependencies {

    implementation project(':shared')

    implementation project(":ui-core:core")
    implementation project(":ui-core:components")
    implementation project(":ui-core:navigation")
    implementation project(":ui-core:sample")

    implementation project(':data-android:database')
    implementation project(':data-android:datastore')
    implementation project(':data-android:domain')
    implementation project(':data-android:location')
    implementation project(':data-android:model')
    implementation project(':data-android:network')
    implementation project(':data-android:repository')

    implementation project(':ui-screens:weather')
    implementation project(':ui-screens:favorites')
    implementation project(':ui-screens:favorites_weather')
    implementation project(':ui-screens:more')
    implementation project(':ui-screens:province')
    implementation project(':ui-screens:city')
    implementation project(':ui-screens:district')
    implementation project(':ui-screens:station')
    implementation project(':ui-screens:settings')
    implementation project(':ui-screens:web')

    testImplementation libs.junit.ext

    // hilt
    implementation libs.hilt.android
    kapt libs.hilt.compiler

    // activity
    implementation libs.androidx.activity.compose

    // style-material3
    implementation libs.style.material3

    // ali location
    implementation libs.ali.location
}